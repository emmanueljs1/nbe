module SystemT where

open import Agda.Builtin.Unit using (âŠ¤; tt)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Data.Product using (_Ã—_; projâ‚; âˆƒ; âˆƒ-syntax) renaming (_,_ to âŸ¨_,_âŸ©)
open import Relation.Nullary using (Dec; yes; no)
open import Relation.Binary.PropositionalEquality using (_â‰¡_)

{- Section 2.1 -- Basic system T -}

-- Types in the language
data Type : Set where
  nat : Type
  _â‡’_ : âˆ€ (S T : Type) â†’ Type

infixr 7 _â‡’_

-- Typing contexts
data Î“ : Set where
  âˆ… : Î“
  _,_ : Î“ â†’ Type â†’ Î“

infixl 5 _,_

-- Lookup judgement for contexts
-- (corresponds to de Brujin indices)
data _âˆ‹_ : Î“ â†’ Type â†’ Set where
  `Z : âˆ€ {Î“ : Î“} {T : Type}
        ---------
      â†’ Î“ , T âˆ‹ T

  `S_ : âˆ€ {Î“ : Î“} {S T : Type}
      â†’ Î“ âˆ‹ T
        ---------
      â†’ Î“ , S âˆ‹ T

infix 4 _âˆ‹_

-- Typing judgement in a context
-- (these correspond to intrinsically typed terms)
data _âŠ¢_ (Î“ : Î“) : Type â†’ Set where
  zero : Î“ âŠ¢ nat

  suc : Î“ âŠ¢ nat â‡’ nat

  rec  : âˆ€ {T : Type}
         ---------------------------------
       â†’ Î“ âŠ¢ (T â‡’ (nat â‡’ T â‡’ T) â‡’ nat â‡’ T)

  `_ : âˆ€ {S : Type}
     â†’ Î“ âˆ‹ S
       -----
     â†’ Î“ âŠ¢ S

  Æ›_ : âˆ€ {S T : Type}
     â†’ Î“ , S âŠ¢ T
       ---------
     â†’ Î“ âŠ¢ S â‡’ T

  _Â·_ : âˆ€ {S T : Type}
      â†’ Î“ âŠ¢ S â‡’ T
      â†’ Î“ âŠ¢ S
        ---------
      â†’ Î“ âŠ¢ T

infix 4 _âŠ¢_
infix 5 Æ›_
infixl 7 _Â·_
infix 9 `_

-- We use the following record to represent interpretations
-- of types and contexts in System T. This will help
-- with the many definitions in the NbE algorithm.
--
-- The original interpretations of types (and of lookup and
-- typing judgements, which are defined independently as
-- functions) are left out, as they need to be updated to
-- work with the final NbE algorithm.
record Denotation (D : Set) : Setâ‚ where
  field
    âŸ¦_âŸ§ : D â†’ Set

open Denotation {{...}} public

instance
    -- The denotation of a context Î“, generalized over
    -- any denotation to be used with the more NbE
    -- specific denotation of types introduced in
    -- later sections
    âŸ¦Î“âŸ§ : {{_ : Denotation Type}} â†’ Denotation Î“
    Denotation.âŸ¦ âŸ¦Î“âŸ§ âŸ§ âˆ… = âŠ¤
    Denotation.âŸ¦ âŸ¦Î“âŸ§ âŸ§ (Î“ , T) = âŸ¦ Î“ âŸ§ Ã— âŸ¦ T âŸ§

{- Section 2.3 -- System T with neutral and normal terms -}

data Ne (T : Type) (Î“ : Î“) : Î“ âŠ¢ T â†’ Set     -- Neutral terms
data Nf : (T : Type) â†’ (Î“ : Î“) â†’ Î“ âŠ¢ T â†’ Set -- Normal terms

data Ne T Î“ where
  -- application on an unknown function
  ne-app : âˆ€ {S : Type} {ğ“Š : Î“ âŠ¢ S â‡’ T} {ğ“‹ : Î“ âŠ¢ S}
         â†’ Ne (S â‡’ T) Î“ ğ“Š
         â†’ Nf S Î“ ğ“‹
           --------------
         â†’ Ne T Î“ (ğ“Š Â· ğ“‹)

  -- a variable is always blocked
  ne-var : (x : Î“ âˆ‹ T)
           ------------
         â†’ Ne T Î“ (` x)

  -- recursion blocked on an unknown natural
  ne-rec : {ğ“‹z : Î“ âŠ¢ T} {ğ“‹s : Î“ âŠ¢ nat â‡’ T â‡’ T} {ğ“Š : Î“ âŠ¢ nat}
         â†’ Nf T Î“ ğ“‹z
         â†’ Nf (nat â‡’ T â‡’ T) Î“ ğ“‹s
         â†’ Ne nat Î“ ğ“Š
           --------------------------
         â†’ Ne T Î“ (rec Â· ğ“‹z Â· ğ“‹s Â· ğ“Š)

data Nf where
  -- zero is a normal term
  nf-zero : âˆ€ {Î“ : Î“} â†’ Nf nat Î“ zero

  -- suc applied to a normal term is a normal term
  nf-suc : âˆ€ {Î“ : Î“} {ğ“‹ : Î“ âŠ¢ nat}
         â†’ Nf nat Î“ ğ“‹
           ------------------
         â†’ Nf nat Î“ (suc Â· ğ“‹)

  -- abstraction over a normal term is a normal term
  nf-abs : âˆ€ {S T : Type} {Î“ : Î“} {ğ“‹ : Î“ , S âŠ¢ T}
         â†’ Nf T (Î“ , S) ğ“‹
           ------------------
         â†’ Nf (S â‡’ T) Î“ (Æ› ğ“‹)

  -- a neutral term is a normal term
  nf-neutral : âˆ€ {T : Type} {Î“ : Î“} {ğ“Š : Î“ âŠ¢ T}
             â†’ Ne T Î“ ğ“Š
               --------
             â†’ Nf T Î“ ğ“Š

{- Section 2.2 -- normalization, definitional equality -}

-- TODO: define
data _defeq_ : âˆ€ {Î“ : Î“} {T : Type} â†’ Î“ âŠ¢ T â†’ Î“ âŠ¢ T â†’ Set where

{- Section 2.5 -- liftable terms, updated NbE algorithm -}

-- Liftable neutral term
data Neâ†‘ (T : Type) : Set where
  neâ†‘ : (âˆ€ (Î“ : Î“) â†’ ((âˆƒ[ t ] Ne T Î“ t) âŠ âŠ¤)) â†’ Neâ†‘ T

-- Liftable normal term
data Nfâ†‘ (T : Type) : Set where
  nfâ†‘ : (âˆ€ (Î“ : Î“) â†’ âˆƒ[ t ] Nf T Î“ t) â†’ Nfâ†‘ T

-- Interpretation of type nat: naturals with embedded
-- liftable neutrals
data â„•Ì‚ : Set where
  zero : â„•Ì‚
  suc : â„•Ì‚ â†’ â„•Ì‚
  ne : Neâ†‘ nat â†’ â„•Ì‚

-- Since the interpretation of System T used in NbE is using
-- liftable neutral and normal terms, we instantiate the
-- denotation of types to use the interpretation of type
-- nat with embedded liftable neutrals (instead of the
-- original â„• in Section 2.1)
instance
  âŸ¦TypeâŸ§ : Denotation Type
  Denotation.âŸ¦ âŸ¦TypeâŸ§ âŸ§ nat = â„•Ì‚
  Denotation.âŸ¦ âŸ¦TypeâŸ§ âŸ§ (S â‡’ T) = âŸ¦ S âŸ§ â†’ âŸ¦ T âŸ§

-- Reflection of neutral terms of type T into
-- semantic objects in âŸ¦TâŸ§
â†‘áµ€ : {T : Type} â†’ Neâ†‘ T â†’ âŸ¦ T âŸ§

-- Reification of semantic objects in âŸ¦TâŸ§ into
-- normal terms of type T
â†“áµ€ : {T : Type} â†’ âŸ¦ T âŸ§ â†’ Nfâ†‘ T -- Reification

-- â†‘á´º - Reflection of neutral terms of type nat into â„•Ì‚,
--      here we just embed the liftable neutral
â†‘áµ€ {nat} ğ“ŠÌ‚ = ne ğ“ŠÌ‚

-- â†‘Ë¢âƒ—áµ— - Reflection of neutral terms of type S â†’ T,
--        into âŸ¦SâŸ§ â†’ âŸ¦TâŸ§. We reify a semantic object in âŸ¦SâŸ§
--        and then reflect the neutral term resulting from the
--        application of the reified object to the original
--        neutral term
â†‘áµ€ {S â‡’ T} (neâ†‘ ğ“Šâ†‘) a with â†“áµ€ a
...  | nfâ†‘ vâ†‘ = â†‘áµ€ (neâ†‘ ğ“ŠÂ·vâ†‘) where
  ğ“ŠÂ·vâ†‘ : âˆ€ (Î“ : Î“) â†’ (âˆƒ[ t ] Ne T Î“ t) âŠ âŠ¤
  ğ“ŠÂ·vâ†‘ Î“ with ğ“Šâ†‘ Î“        | vâ†‘ Î“
  ... | injâ‚ âŸ¨ ğ“Š , pf-ğ“Š âŸ© | âŸ¨ ğ“‹ , pf-ğ“‹ âŸ© = injâ‚ âŸ¨ ğ“Š Â· ğ“‹ , ne-app pf-ğ“Š pf-ğ“‹ âŸ©
  ... | injâ‚‚ tt           | _ = injâ‚‚ tt

-- Rules for determining when one context is the
-- extension of another.
data _Î“-â‰¤_ : Î“ â†’ Î“ â†’ Set where
  âˆ…-â‰¤ : âˆ€ {Î“ : Î“}
        ---------
       â†’ Î“ Î“-â‰¤ âˆ…

  ,-â‰¤ : âˆ€ {Î“ Î“â€² : Î“} {T : Type}
      â†’ Î“â€² Î“-â‰¤ Î“
        ------------
      â†’ Î“â€² , T Î“-â‰¤ Î“

infix 4 _Î“-â‰¤_

_Î“-â‰¤?_ : âˆ€ (Î“â€² Î“ : Î“) â†’ Dec (Î“â€² Î“-â‰¤ Î“)
âˆ… Î“-â‰¤? âˆ… = yes âˆ…-â‰¤
âˆ… Î“-â‰¤? (_ , _) = no Î»()
(_ , _) Î“-â‰¤? âˆ… = yes âˆ…-â‰¤
(Î“â€² , _) Î“-â‰¤? Î“@(_ , _) with Î“â€² Î“-â‰¤? Î“
... | yes pf  = yes (,-â‰¤ pf)
... | no Â¬pf  = no Î»{ (,-â‰¤ pf) â†’ Â¬pf pf }

-- Given one context is an extension of another, and a
-- lookup judgement in the original context, there
-- is a corresponding lookup judgement in the extended context.
lookup-Î“-â‰¤ : âˆ€ {Î“â€² Î“ : Î“} {T : Type}
           â†’ Î“â€² Î“-â‰¤ Î“
           â†’ Î“ âˆ‹ T
             --------
           â†’ Î“â€² âˆ‹ T
lookup-Î“-â‰¤ (,-â‰¤ pf) i
  with lookup-Î“-â‰¤ pf i
... | j = `S j

-- Create a new lifted variable of type S in the context Î“,
-- which can only be applied to extensions of Î“ , S
mk-lifted-var : âˆ€ {S : Type} (Î“â‚ : Î“) â†’ Neâ†‘ S
mk-lifted-var {S} Î“â‚ = neâ†‘ varâ†‘ where
  varâ†‘ : âˆ€ (Î“â‚‚ : Î“) â†’ (âˆƒ[ t ] Ne S Î“â‚‚ t) âŠ âŠ¤
  varâ†‘ Î“â‚‚ with Î“â‚‚ Î“-â‰¤? (Î“â‚ , S)
  ... | yes pf  = injâ‚ âŸ¨ ` x , ne-var x âŸ© where x = lookup-Î“-â‰¤ pf `Z
  ... | no _    = injâ‚‚ tt

-- â†“á´º - Reification of semantic objects of type âŸ¦NâŸ§, which
--      are our naturals with embedded liftable neutrals (â„•Ì‚).
--      For the interesting case of embedded liftable neutrals,
--      zero is used if the neutral cannot be lifted to the
--      context Î“
â†“â„•Ì‚ : âŸ¦ nat âŸ§ â†’ Nfâ†‘ nat
â†“â„•Ì‚ zero = nfâ†‘ (Î» _ â†’ âŸ¨ zero , nf-zero âŸ©)
â†“â„•Ì‚ (suc n) with â†“â„•Ì‚ n
... | nfâ†‘ nâ†‘ = nfâ†‘ sucâ†‘ where
  sucâ†‘ : (Î“ : Î“) â†’ âˆƒ[ t ] Nf nat Î“ t
  sucâ†‘ Î“ with nâ†‘ Î“
  ... | âŸ¨ ğ“‹ , pf âŸ© = âŸ¨ suc Â· ğ“‹ , nf-suc pf âŸ©
â†“â„•Ì‚ (ne (neâ†‘ ğ“Šâ†‘)) = nfâ†‘ ğ“ŠÌ‚ where
  ğ“ŠÌ‚ : âˆ€ (Î“ : Î“) â†’ âˆƒ[ t ] Nf nat Î“ t
  ğ“ŠÌ‚ Î“ with ğ“Šâ†‘ Î“
  ... | injâ‚ âŸ¨ ğ“Š , pf âŸ© = âŸ¨ ğ“Š , nf-neutral pf âŸ©
  ... | injâ‚‚ tt         = âŸ¨ zero , nf-zero âŸ©

â†“áµ€ {nat} = â†“â„•Ì‚

-- â†“Ë¢âƒ—áµ— - Reification of semantic objects of type âŸ¦S â†’ TâŸ§,
--        which are functions of type (âŸ¦SâŸ§ â†’ âŸ¦TâŸ§). The
--        resulting normal term is an abstraction over
--        the reification of the function applied to the
--        reflection of the bound variable
â†“áµ€ {S â‡’ T} f = nfâ†‘ fâ†‘ where
  fâ†‘ : âˆ€ (Î“ : Î“) â†’ âˆƒ[ t ] Nf (S â‡’ T) Î“ t
  fâ†‘ Î“ with â†“áµ€ (f a) where a = â†‘áµ€ (mk-lifted-var Î“)
  ... | nfâ†‘ fÂ·aâ†‘
      with fÂ·aâ†‘ (Î“ , S)
  ... | âŸ¨ ğ“‹ , pf âŸ© = âŸ¨ Æ› ğ“‹ , nf-abs pf âŸ©

-- Reflection of a context gamma
â†‘Î“ : âˆ€ (Î“ : Î“) â†’ âŸ¦ Î“ âŸ§
â†‘Î“ âˆ… = tt
â†‘Î“ (Î“ , T) = âŸ¨ â†‘Î“ Î“  , â†‘áµ€ {T} (mk-lifted-var Î“) âŸ©

-- Denotation of primitive recursion in language,
-- updated in section 2.5 from the basic denotation
-- to handle the new case of recursion being over
-- an embedded liftable neutral by reflecting a
-- "liftable" recursion over a liftable neutral term

-- Note: the original habilitation has the type of the first
-- argument to rec as "N" (nat), this seems to be a typo
âŸ¦recâŸ§ : âˆ€ {T : Type} â†’ âŸ¦ T â‡’ (nat â‡’ T â‡’ T) â‡’ nat â‡’ T âŸ§
âŸ¦recâŸ§ z s zero = z
âŸ¦recâŸ§ z s (suc n) = s n (âŸ¦recâŸ§ z s n)
âŸ¦recâŸ§ {T} z s (ne (neâ†‘ uâ†‘)) = â†‘áµ€ (neâ†‘ recâ†‘) where
  recâ†‘ : âˆ€ (Î“ : Î“) â†’ âˆƒ[ t ] Ne T Î“ t âŠ âŠ¤
  recâ†‘ Î“ with uâ†‘ Î“
  ... | injâ‚‚ tt = injâ‚‚ tt
  ... | injâ‚ âŸ¨ ğ“Š , pf-ğ“Š âŸ©
         with â†“áµ€ z | â†“áµ€ s
  ... | nfâ†‘ zâ†‘     | nfâ†‘ sâ†‘
        with zâ†‘ Î“      | sâ†‘ Î“
  ... | âŸ¨ ğ“‹z , pf-ğ“‹z âŸ© | âŸ¨ ğ“‹s , pf-ğ“‹s âŸ© = injâ‚ âŸ¨ rec Â· ğ“‹z Â· ğ“‹s Â· ğ“Š , ne-rec pf-ğ“‹z pf-ğ“‹s pf-ğ“Š âŸ©

-- And the corresponding denotations of the
-- lookup and typing judgements.
--
-- These are not directly shown in section 2.5, but they
-- are very similar to their counterparts in section 2.1

âˆ‹âŸ¦_âŸ§ : âˆ€ {Î“ : Î“} {T : Type} â†’ Î“ âˆ‹ T â†’ âŸ¦ Î“ âŸ§ â†’ âŸ¦ T âŸ§
âˆ‹âŸ¦_âŸ§ {Î“ , T} `Z âŸ¨ _ , a âŸ© = a
âˆ‹âŸ¦_âŸ§ {Î“ , T} (`S x) âŸ¨ Ï , _ âŸ© = âˆ‹âŸ¦ x âŸ§ Ï

âŠ¢âŸ¦_âŸ§ : âˆ€ {Î“ : Î“} {T : Type} â†’ Î“ âŠ¢ T â†’ âŸ¦ Î“ âŸ§ â†’ âŸ¦ T âŸ§
âŠ¢âŸ¦ zero âŸ§ _ = zero
âŠ¢âŸ¦ suc âŸ§ _ = suc
âŠ¢âŸ¦ rec âŸ§ _ = âŸ¦recâŸ§
âŠ¢âŸ¦ ` x âŸ§ = âˆ‹âŸ¦ x âŸ§
âŠ¢âŸ¦ Æ› t âŸ§ Ï a = âŠ¢âŸ¦ t âŸ§ âŸ¨ Ï , a âŸ©
âŠ¢âŸ¦ r Â· s âŸ§ Ï = âŠ¢âŸ¦ r âŸ§ Ï (âŠ¢âŸ¦ s âŸ§ Ï)

-- Finally, the algorithm for normalization by evaluation
nbe : âˆ€ {Î“ : Î“} {T : Type} â†’ Î“ âŠ¢ T â†’ âˆƒ[ t ] Nf T Î“ t
nbe {Î“} t with â†“áµ€ (âŠ¢âŸ¦ t âŸ§ (â†‘Î“ Î“))
... | nfâ†‘ tâ†‘ = tâ†‘ Î“

nf : âˆ€ {Î“ : Î“} {T : Type} â†’ Î“ âŠ¢ T â†’ Î“ âŠ¢ T
nf t with nbe t
... | âŸ¨ tâ€² , _ âŸ© = tâ€²

-- There are a few properties we want from this algorithm:
--   - Î“ âŠ¢ nf(t) : T (well-typedness)
--   - âŸ¦ nf(t) âŸ§ = âŸ¦ t âŸ§ (preservation of meaning)
--   - nf(nf(t)) = nf(t) (idempotency)

-- As we are using an intrinsically typed representation of terms, the first
-- property is given to us automatically.

-- For preservation of meaning and idempotency, we have the following:

-- TODO: prove?
postulate
  preserve-meaning : âˆ€ {Î“ : Î“} {T : Type} {t : Î“ âŠ¢ T} â†’ âŠ¢âŸ¦ nf t âŸ§ â‰¡ âŠ¢âŸ¦ t âŸ§
  idempotent : âˆ€ {Î“ : Î“} {T : Type} {t : Î“ âŠ¢ T} â†’ nf (nf t) â‰¡ nf t

{- Section 2.6 -- Soundness -}

-- We want to prove the soundness of normalization,
-- i.e. Î“ âŠ¢ t = nf(t) : T (there is a definitional
-- equality between t and nf(t) as determined by
-- Î²Î·-equivalence extended with rules characterizing
-- the computational behavior of primitive recursion,
-- as explained in section 2.2)
--
-- For this, a logical relation â“‡ is defined such that
-- it implies Î“ âŠ¢ t = nf(t) : T

-- First, we define a function for mapping a well-typed term in a
-- context Î“ to a well-typed term in an extension of Î“, the context Î“â€²
-- TODO: look at exts/subst in DeBrujin section of PLFA
ext : âˆ€ {Î“ Î“â€² : Î“} {T : Type} â†’ Î“â€² Î“-â‰¤ Î“ â†’ Î“ âŠ¢ T â†’ Î“â€² âŠ¢ T
ext = {!!}

-- The Kripe logical relation

_â“‡_ : âˆ€ {Î“ : Î“} {T : Type} â†’ Î“ âŠ¢ T â†’ âŸ¦ T âŸ§ â†’ Set

_â“‡_ {Î“â‚} {nat} t ğ•ŸÌ‚ with â†“â„•Ì‚ ğ•ŸÌ‚
... | nfâ†‘ vâ†‘ =
  âˆ€ {Î“â‚‚ : Î“}
  â†’ (pf : Î“â‚‚ Î“-â‰¤ Î“â‚)
  â†’ (ext pf t) defeq (projâ‚ (vâ†‘ Î“â‚‚))

_â“‡_ {Î“â‚} {S â‡’ T} r f =
  âˆ€ {Î“â‚‚ : Î“} {s : Î“â‚‚ âŠ¢ S} {a : âŸ¦ S âŸ§}
  â†’ (pf : Î“â‚‚ Î“-â‰¤ Î“â‚)
  â†’ s â“‡ a
  â†’ ((ext pf r) Â· s) â“‡ (f a)
